<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="DEAD_BAND_A" Id="{db4da26a-aa57-4830-acb7-617d5c7f0f8a}" SpecialFunc="None">
    <Declaration><![CDATA[(*
version 1.1	11. mar. 2009
programmer 	hugo
tested by		oscat

DEAD_BAND is a linear transfer function with a dead zone. The function shifts the positive part of the curve by -L and the negative part by +L.
DEAD_BAND = X when Abs(X) > L
DEAD_BAND = 0 when Abs(X) <= L
The width of the dead zone is automatically calculated from the signal noise.

*)
FUNCTION_BLOCK DEAD_BAND_A
VAR_INPUT
	X : REAL;
	T : TIME;
	KL : REAL := 1.0;
	LM : REAL;
END_VAR
VAR_OUTPUT
	Y : REAL;
	L : REAL;
END_VAR
VAR
	tp1 : FT_PT1;
	tp2 : FT_PT1;
END_VAR


]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* filter the input signal *)
tp1(in := X, T:= T);

(* filter the HF portion to generate a stable L *)
tp2(in := ABS(tp1.out - X), T := MULTIME(T, 4.0));

(* now we determine L which is half the width of the dead band *)
L := MIN(KL * tp2.out, LM);

IF X > L THEN
	Y := X - L;
ELSIF X < -L THEN
	Y := X + L;
ELSE
	Y := 0.0;
END_IF;


(* revision history
hm	14. jun. 2008	rev 1.0
	original version

hm	11. mar. 2009	rev 1.1
	real constants updated to new systax using dot

*)]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>