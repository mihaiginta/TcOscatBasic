<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="REPLACE_CHARS" Id="{3e06dded-49df-409f-b31e-753a68270434}" SpecialFunc="None">
    <Declaration><![CDATA[(*
version 1.0		14. may. 2008
programmer 		hugo
tested by			hugo

REPLACE_CHARS replaces all characters listed in the string src with the character listed at the same position in the string rep.

*)
FUNCTION REPLACE_CHARS : STRING(STRING_LENGTH)
VAR_INPUT
	STR : STRING(STRING_LENGTH);
	SRC : STRING;
	REP : STRING;
END_VAR
VAR
	a, b : INT;
	c: STRING(1);
	stp: INT;
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[REPLACE_CHARS := STR;
(* make sure rep and src are of same length and length is > 0 *)
a := LEN(src);
b := LEN(rep);
IF a < b THEN
	rep := LEFT(rep, a);
ELSIF b < a THEN
	src := LEFT(src, b);
END_IF;

(* search the string and replace if necessary *)
stp := LEN(str);
FOR a := 1 TO stp DO
	c := MID(REPLACE_CHARS, 1, a);
	b := FIND(src, c);
	IF b > 0 THEN
		REPLACE_CHARS := REPLACE(REPLACE_CHARS, MID(rep, 1, b), 1, a);
	END_IF;
END_FOR;



(* revision history
hm	14. may. 2008	rev 1.0
	original version

*)
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>